buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

sourceCompatibility = targetCompatibility = "1.8"
version = "1.12.2-" + getVersion()
group = "minechem"
archivesBaseName = "MineChem"

minecraft {
    version = "1.12.2-14.23.4.2726"
    runDir = "run"
    mappings = "snapshot_20171003"
	replace '@FINGERPRINT@', project.findProperty('signSHA1')
}

repositories {
    mavenCentral()
	maven {
		name = "progwml6"
		url = "http://dvs1.progwml6.com/files/maven"
	}
	maven {
		name "CurseForge"
		url "https://minecraft.curseforge.com/api/maven/"
	}
	maven {
		name = "tterrag"
		url = "http://maven.tterrag.com"
	}
	maven {
		name = "tterrag2"
		url = "http://maven2.tterrag.com"
	}
	maven {
        name = "covers Maven"
        url = "http://maven.covers1624.net"
    }
}

dependencies {
	deobfCompile "mezz.jei:jei_1.12.2:4.14.3.238"
	compile "mezz.jei:jei_1.12.2:4.14.3.238"
	runtime "mezz.jei:jei_1.12.2:4.14.3.238"
	deobfCompile "codechicken:CodeChickenLib:1.12.2-3.2.2.353:universal"
	compile "codechicken:CodeChickenLib:1.12.2-3.2.2.353:universal"
	deobfCompile "codechicken:NotEnoughItems:1.12.2-2.4.2.240:universal"
	deobfCompile "cofh:RedstoneFlux:1.12-2.1.0.6:universal"
	deobfCompile "cofh:ThermalDynamics:1.12.2-2.5.4.18:universal"
	deobfCompile "cofh:ThermalFoundation:1.12.2-2.6.2.26:universal"
	deobfCompile "cofh:ThermalExpansion:1.12.2-5.5.3.41:universal"
	deobfCompile "cofh:CoFHCore:1.12.2-4.6.2.25:universal"
	deobfCompile "cofh:CoFHWorld:1.12.2-1.3.0.6:universal"
	deobfCompile "laggoggles:LagGoggles-FORGE-1.12.2:4.3:HOTFIX"
	deobfCompile "com.enderio.core:EnderCore:1.12.2-0.5.43"
	deobfCompile ("com.enderio:EnderIO:1.12.2-5.0.38") {
		transitive = false
	}
	deobfCompile "info.loenwind.autoconfig:AutoConfig:1.12.2-1.0.1"
	deobfCompile "info.loenwind.autosave:AutoSave:1.12.2-1.0.8"
	deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.23-16"
	deobfCompile "p455w0rd:p455w0rdslib:1.12.2-2.2.101"
}

task apiJar(type: Jar) {
	classifier = 'api'
	from sourceSets.api.output
	from sourceSets.api.allJava
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar

artifacts {
	archives apiJar
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
	rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
	manifest {
	    attributes 'FMLAT': 'minechem_at.cfg'
	}
}

String getVersion() {
	String major = "0";
	String revision = "0";
	String patch = "0";
	String prefix = "public static final String VERSION = \"";
	File file = file("src/main/java/minechem/init/ModGlobals.java")
	file.eachLine { String s ->
		s = s.trim();
		if (s.startsWith(prefix)) {
			s = s.substring(prefix.length(), s.length() - 2);
			String[] pts = s.split("\\.");

			major = pts[0];
			revision = pts[1];
			patch = pts[2];
		}
	}
	return "$major.$revision.$patch";
}
